plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'mukherjee.cameron'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2020.0.4")
}

dependencies {
    api 'org.springframework.cloud:spring-cloud-starter-kubernetes-client:2.0.3'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

publishing {
    publications {
        'astute4j'(MavenPublication) {
            from(components.java)
            groupId = project.group
            artifactId = 'astute4j'
            version = project.version

            pom {
                name = 'astute4j'
                description = 'astute4j'
                url = 'https://github.com/cameronmukherjee/astute4j'
            }
        }
    }

    repositories {
        maven {
            name = 'astute4j'
            url = uri('https://maven.pkg.github.com/cameronmukherjee/astute4j')
            credentials {
                username = System.getenv("GIT_USERNAME") ?: project.findProperty("GIT_USERNAME")
                password = System.getenv("GIT_TOKEN") ?: project.findProperty("GIT_TOKEN")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
